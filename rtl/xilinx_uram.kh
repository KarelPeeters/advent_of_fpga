pub external module xilinx_ultraram_true_dual_port(
    AWIDTH: natural = 12,
    DWIDTH: natural = 72,
    NBPIPE: natural = 3,
) ports(
    clk: in clock,

    sync(clk) {
        wea: in bool,
        mem_ena: in bool,
        dina: in [DWIDTH]bool,
        addra: in [AWIDTH]bool,
        douta: out [DWIDTH]bool,

        web: in bool,
        mem_enb: in bool,
        dinb: in [DWIDTH]bool,
        addrb: in [AWIDTH]bool,
        doutb: out [DWIDTH]bool,
    }
)

/// The underlying module does not actually have a reset, we just add it here for convenience.
pub module uram(N: uint, T: type, P: uint) ports(
    clk: in clock,
    rst: in async bool,

    sync(clk, rst) {
        port_a: interface uram_port(T, N).input,
        port_b: interface uram_port(T, N).input,
    }
) {
    const AWIDTH = clog2(N);
    const DWIDTH = T.size_bits;

    wire read_data_a: [DWIDTH]bool;
    wire read_data_b: [DWIDTH]bool;

    instance xilinx_ultraram_true_dual_port(
        AWIDTH=AWIDTH,
        DWIDTH=DWIDTH,
        NBPIPE=P,
    ) ports(
        clk,

        wea=unsafe_value_with_domain(port_a.write_enable, sync(clk)),
        mem_ena=unsafe_value_with_domain(port_a.mem_enable, sync(clk)),
        dina=unsafe_value_with_domain(T.to_bits(port_a.write_data), sync(clk)),
        addra=unsafe_value_with_domain(uint(AWIDTH).to_bits(port_a.address), sync(clk)),
        douta=read_data_a,

        web=unsafe_value_with_domain(port_b.write_enable, sync(clk)),
        mem_enb=unsafe_value_with_domain(port_b.mem_enable, sync(clk)),
        dinb=unsafe_value_with_domain(T.to_bits(port_b.write_data), sync(clk)),
        addrb=unsafe_value_with_domain(uint(AWIDTH).to_bits(port_b.address), sync(clk)),
        doutb=read_data_b,
    );

    comb {
        port_a.read_data = T.from_bits_unsafe(read_data_a);
        port_b.read_data = T.from_bits_unsafe(read_data_b);
    }
}

pub interface uram_port(T: type, N: uint) {
    mem_enable: bool,
    write_enable: bool,
    address: uint(0..N),
    write_data: T,
    read_data: T,

    interface input {
        mem_enable: in,
        write_enable: in,
        address: in,
        write_data: in,
        read_data: out,
    }
}

const {
    uram(T=bool, N=2, P=3);
    uram(T=uint(0..3), N=2, P=3);
}
